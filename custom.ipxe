#!ipxe
###
### RogueScholar iPXE Menu
###

#Wish I could dress up my menu but Netboot.xyz doesn't compile in the console command
#console --x 1280 --y 800
#console --picture http://boot.ipxe.org/texture.png --left 240 --right 240 --top 80 --bottom 120 --depth 24

:start
echo Greetings, wayfaring stranger.
:custom
clear custom_choice
menu Select the desired boot environment
item --gap Parted Magic (v2020_05_20)
item pmagicnet64 ${space} x86_64 netsrc+bzImage64/initrd
item pmagicnet32 ${space} i386 netsrc+bzImage/initrd
item pmagic64    ${space} x86_64 classic+bzImage64/initrd
item pmagic32    ${space} i386 classic+bzImage/initrd
item --gap Other utilities
item grmliso ${space} Grml 2018.12 amd64 Full ISO
item grmlram ${space} Grml 2018.12 amd64 Full ramdisk
item czillaiso ${space} Clonezilla Live 20191009 amd64 Ubuntu Eoan ISO
item czillaram ${space} Clonezilla Live 20191009 amd64 Ubuntu Eoan ramdisk
choose custom_choice || goto custom_exit
echo ${cls}
goto ${custom_choice}
goto custom_exit

:pmagicnet64
set pmagic-path https://rscholar.keybase.pub/ipxe/pmagic
imgfetch --name bzImage64 ${pmagic-path}/bzImage64?dl=1
imgfetch --name initrd.img ${pmagic-path}/initrd.img?dl=1
imgfetch --name fu.img ${pmagic-path}/fu.img?dl=1
imgfetch --name m64.img ${pmagic-path}/m64.img?dl=1
chain bzImage64 initrd=initrd.img initrd=fu.img initrd=m64.img edd=on vga=normal netsrc=wget neturl="https://keybase.pub/rscholar/ipxe/pmagic/pmodules/" netargs="-U pmagicboot" boot=live ip=dhcp load_ramdisk=1 prompt_ramdisk=0 rw sleep=0 loglevel=0 keymap=us nox2apic || goto custom_exit

:pmagicnet32
set pmagic-path https://rscholar.keybase.pub/ipxe/pmagic
imgfetch --name bzImage ${pmagic-path}/bzImage?dl=1
imgfetch --name initrd.img ${pmagic-path}/initrd.img?dl=1
imgfetch --name fu.img ${pmagic-path}/fu.img?dl=1
imgfetch --name m32.img ${pmagic-path}/m32.img?dl=1
chain bzImage initrd=initrd.img initrd=fu.img initrd=m32.img edd=on vga=normal netsrc=wget neturl="https://keybase.pub/rscholar/ipxe/pmagic/pmodules/" netargs="-U pmagicboot" boot=live ip=dhcp load_ramdisk=1 prompt_ramdisk=0 rw sleep=0 loglevel=0 keymap=us nox2apic || goto custom_exit

:pmagic64
set pmagic-path https://rscholar.keybase.pub/ipxe/pmagic
imgfetch --name bzImage64 ${pmagic-path}/bzImage64?dl=1
imgfetch --name initrd.img ${pmagic-path}/initrd.img?dl=1
imgfetch --name fu.img ${pmagic-path}/fu.img?dl=1
imgfetch --name m64.img ${pmagic-path}/m64.img?dl=1
imgfetch --name files.cgz ${pmagic-path}/files.cgz?dl=1
chain bzImage64 initrd=initrd.img initrd=fu.img initrd=m64.img initrd=files.cgz boot=live ip=dhcp edd=on load_ramdisk=1 prompt_ramdisk=0 rw vga=normal sleep=0 loglevel=0 keymap=us nox2apic || goto custom_exit

:pmagic32
set pmagic-path https://rscholar.keybase.pub/ipxe/pmagic
imgfetch --name bzImage ${pmagic-path}/bzImage?dl=1
imgfetch --name initrd.img ${pmagic-path}/initrd.img?dl=1
imgfetch --name fu.img ${pmagic-path}/fu.img?dl=1
imgfetch --name m32.img ${pmagic-path}/m32.img?dl=1
imgfetch --name files.cgz ${pmagic-path}/files.cgz?dl=1
chain bzImage initrd=initrd.img initrd=fu.img initrd=m32.img initrd=files.cgz boot=live ip=dhcp edd=on load_ramdisk=1 prompt_ramdisk=0 rw vga=normal sleep=0 loglevel=0 keymap=us splash nox2apic || goto custom_exit

:grmliso
set grml-path https://rscholar.keybase.pub/ipxe/grml
imgfetch --name memdisk ${grml-path}/memdisk?dl=1
imgfetch --name grml64-full_2018.12.iso ${grml-path}/grml64-full_2018.12.iso?dl=1
chain memdisk initrd=grml64-full_2018.12.iso raw iso nox2apic vmalloc=256M
boot || goto custom_exit

:grmlram
set grml-path https://rscholar.keybase.pub/ipxe/grml
imgfetch --name vmlinuz ${grml-path}/vmlinuz?dl=1
imgfetch --name initrd.img ${grml-path}/initrd.img?dl=1
imgfetch --name grml64-full.squashfs ${grml-path}/grml64-full.squashfs
chain vmlinuz initrd=initrd.img initrd=grml64-full.squashfs boot=live apm=power-off vga=791 nomce noeject noprompt noblank keyboard=us xkeyboard=us tz=America/Los_Angeles hostname ssh=password nox2apic || goto custom_exit

:czillaiso
set czilla-path https://rscholar.keybase.pub/ipxe/czilla
imgfetch --name memdisk ${czilla-path}/memdisk?dl=1
imgfetch --name clonezilla-live-20191009-eoan-amd64.iso ${czilla-path}/clonezilla-live-20191009-eoan-amd64.iso?dl=1
chain memdisk initrd=clonezilla-live-20191009-eoan-amd64.iso raw iso nox2apic vmalloc=256M || goto custom_exit

:czillaram
set czilla-path https://rscholar.keybase.pub/ipxe/czilla
imgfetch --name vmlinuz ${czilla-path}/vmlinuz?dl=1
imgfetch --name initrd.img ${czilla-path}/initrd.img?dl=1
imgfetch --name filesystem.squashfs ${czilla-path}/filesystem.squashfs
chain vmlinuz initrd=initrd.img initrd=filesystem.squashfs boot=live username=user union=overlay config components quiet noswap edd=on nodmraid locales=en_US.UTF-8 ocs_live_run="ocs-live-general" ocs_live_batch=no net.ifnames=0 nosplash noprompt nox2apic || goto custom_exit

sleep 300
goto custom_exit

:custom_exit
exit 0

#chain https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/custom1.ipxe || goto custom
